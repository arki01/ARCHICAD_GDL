
!======================================================================================================
!	창호 20 - 프레임(도어)
!======================================================================================================
!	DATE : 2018/1/22 MON
!	VER : AC 20
!	DEV : JO HYUNG JUN



!----------------------------------------------------------------------------------------------------
!	Test
!----------------------------------------------------------------------------------------------------
define style "style_test_3d"	"맑은 고딕", 50, 1, 0
set style	"style_test_3d"
if	GLOB_VIEW_TYPE = 3	then
	!	text		 0.001, 0, stMyname
endif

!	원점
if	bOrigin	then
	sphere 0.005
	hotspot	0, 0, 0
endif



!----------------------------------------------------------------------------------------------------
!	공통 
!----------------------------------------------------------------------------------------------------
unID = 1000

define style "style_index"	"맑은 고딕", 50, 1, 0
define style "style_text"	"맑은 고딕", 50, 5, 0

!----------------------------------------------------------------------------------------------------
!	형상
!----------------------------------------------------------------------------------------------------
resol iResol
pen	iFrame_pen

	!	단순 표현
	!----------------------------------------------------------------------------------------------------
	if	iDetail_3d = DETAIL_3D_SIMPLE	then

		!	핫스팟
		!----------------------------------------------------------------------------------------------------
		hotspot	0, 0, 0, 			unID	: unID = unID + 1
		hotspot	mWidth, 0, 0, 		unID	: unID = unID + 1	
		hotspot	mWidth, 0, mHeight, unID	: unID = unID + 1
		hotspot	0, 0, mHeight,		unID	: unID = unID + 1	

		!	프레임
		!----------------------------------------------------------------------------------------------------
		!	수직 프레임
		_mFrmL = mHeight
		gosub	"frame_shape_vert"
		addx	mWidth
			mulx	-1
				_mFrmL = mHeight
				gosub	"frame_shape_vert"
			del	1
		del	1

		!	상부 수평 프레임
		addz	mHeight
			_mFrmL = mWidth
			gosub	"frame_shape_hori_top"
		del	1

	endif

	!	상세 표현
	!----------------------------------------------------------------------------------------------------
	if	iDetail_3d = DETAIL_3D_DETAIL	then
		add		mWidth/2, 0, mHeight/2
			rotx	90	
				set style	"style_text"
				material iText_mat
				text	0.001, 0, "상세표현 개발 예정"
			del	1
		del	1
	endif


!======================================================================================================
end
!======================================================================================================


"frame_shape_vert"	:

	!	수직 프레임이 기준
	material iFrame_mat
	sect_fill	iFrame_sect_fill, iFrame_sect_back_pen, iFrame_sect_fill_pen, iFrame_sect_pen

	si = 1+2+4+8

	put 0, 0, si,
		mFrame_top_w1, 0, si,
		mFrame_top_w1, mFrame_top_d1, si,
		mFrame_top_w, mFrame_top_d1, si,
		mFrame_top_w, mFrame_top_d1+mFrame_top_d2, si,
		mFrame_top_w1, mFrame_top_d1+mFrame_top_d2, si,
		mFrame_top_w1, mFrame_top_d, si,
		0, mFrame_top_d, si,
		0, 0, si

	addz	_mFrmL
	roty	45
		cutplane
	del	1
	del	1

		prism_    nsp/3, _mFrmL, get(nsp)

	cutend

return


"frame_shape_hori_top"	:

	roty	180+45
		cutplane
	del	1

	roty	90
		gosub	"frame_shape_vert"
	del	1

	cutend	

return


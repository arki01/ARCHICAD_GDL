
!======================================================================================================
!	창호 20 - 프레임(창호)
!======================================================================================================
!	DATE : 2018/2/7 WED
!	VER : AC 20
!	DEV : JO HYUNG JUN



!----------------------------------------------------------------------------------------------------
!	Test
!----------------------------------------------------------------------------------------------------
define style "style_test_3d"	"맑은 고딕", 50, 1, 0
set style	"style_test_3d"
if	GLOB_VIEW_TYPE = 3	then
	!	text		 0.001, 0, stMyname
endif

!	원점
if	bOrigin	then
	sphere 0.005
	hotspot	0, 0, 0
endif



!----------------------------------------------------------------------------------------------------
!	공통 
!----------------------------------------------------------------------------------------------------
unID = 1000

define style "style_index"	"맑은 고딕", 50, 1, 0
define style "style_text"	"맑은 고딕", 50, 5, 0

!----------------------------------------------------------------------------------------------------
!	형상
!----------------------------------------------------------------------------------------------------
resol iResol
pen	iFrame_pen

	!	단순 표현
	! !----------------------------------------------------------------------------------------------------
	if	iDetail_3d = DETAIL_3D_SIMPLE | iDetail_3d = DETAIL_3D_DETAIL	then

		!	핫스팟
		!----------------------------------------------------------------------------------------------------
		hotspot	0, 0, 0, 			unID	: unID = unID + 1
		hotspot	mWidth, 0, 0, 		unID	: unID = unID + 1	
		hotspot	mWidth, 0, mHeight, unID	: unID = unID + 1
		hotspot	0, 0, mHeight,		unID	: unID = unID + 1	

		!	프레임
		!----------------------------------------------------------------------------------------------------
		!	상부 프레임

			!	수직 프레임
			!----------------------------------------------------------------------------------------------------
			if	iFrame_setting = FRAME_SIZE_ONCE	then
				_mFrmW = mFrame_w
				_mFrmD = mFrame_d
			endif
			if	iFrame_setting = FRAME_SIZE_VERTHORI	then
				_mFrmW = mFrame_vert_w
				_mFrmD = mFrame_vert_d
			endif
			_mFrmL = mHeight

			gosub	"frame_shape_vert"
			addx	mWidth
				mulx	-1
					_mFrmL = mHeight
					gosub	"frame_shape_vert"
				del	1
			del	1

			!	수평 프레임
			!----------------------------------------------------------------------------------------------------
			if	iFrame_setting = FRAME_SIZE_ONCE	then
				_mFrmW = mFrame_w
				_mFrmD = mFrame_d
				_mFrm_offsetx = mFrame_w
				_mFrm_offsetz = mFrame_w				
			endif
			if	iFrame_setting = FRAME_SIZE_VERTHORI	then
				_mFrmW = mFrame_hori_w
				_mFrmD = mFrame_hori_d
				_mFrm_offsetx = mFrame_vert_w
				_mFrm_offsetz = mFrame_hori_w
			endif

			!	- 45도 접합 
			if	iFrame_joint = FRAME_JOINT_45CUT	then
				_mFrmL = mWidth
				gosub	"frame_shape_hori"

				addz	mHeight
					mulz	-1
						gosub	"frame_shape_hori"
					del	1
				del	1
			endif

			!	- 90도 접합 
			if	iFrame_joint = FRAME_JOINT_90CUT	then
				_mFrmL = mWidth - _mFrm_offsetx*2
				addx	_mFrm_offsetx
					gosub	"frame_shape_hori"

					addz	mHeight - _mFrm_offsetz
						gosub	"frame_shape_hori"
					del	1
				del	1
			endif

	endif

	! !	상세 표현
	! !----------------------------------------------------------------------------------------------------
	! if	iDetail_3d = DETAIL_3D_DETAIL	then
	! 	add		mWidth/2, 0, mHeight/2
	! 		rotx	90	
	! 			set style	"style_text"
	! 			material iText_mat
	! 			text	0.001, 0, "상세표현 개발 예정"
	! 		del	1
	! 	del	1
	! endif


!======================================================================================================
end
!======================================================================================================


"frame_shape_vert"	:

	material iFrame_mat
	sect_fill	iFrame_sect_fill, iFrame_sect_back_pen, iFrame_sect_fill_pen, iFrame_sect_pen

	si = 1+2+4+8

	put 0, 0, si,
		_mFrmW, 0, si,
		_mFrmW, _mFrmD, si,
		0, _mFrmD, si


	if	iFrame_joint = FRAME_JOINT_45CUT	then
		roty	45+90
			cutplane
		del	1

		addz	_mFrmL
			roty	45
				cutplane
			del	1
		del	1
	endif

	prism_    nsp/3, _mFrmL, get(nsp)

	if	iFrame_joint = FRAME_JOINT_45CUT	then
		cutend
		cutend
	endif

return


"frame_shape_hori"	:

	mulz	-1
		roty	90
			gosub	"frame_shape_vert"
		del	1
	del	1

	
return
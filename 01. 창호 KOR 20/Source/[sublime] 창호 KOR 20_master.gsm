
!======================================================================================================
!	창호 KOR 20
!======================================================================================================
!   DATE : 2018/02/07 WED
!   VER : AC 20
!	DEV : JO HYUNG JUN


!----------------------------------------------------------------------------------------------------
!	Test
!----------------------------------------------------------------------------------------------------
n = request("name_of_macro", "", stMyname)


!----------------------------------------------------------------------------------------------------
!	공통
!----------------------------------------------------------------------------------------------------
EPS = 0.0001

	!	전체치수
	!----------------------------------------------------------------------------------------------------
	!	'iDim' 인덱스 - 마감 매크로에 포함되어있음
	DIM1 = 1 
	DIM2 = 2 
	DIM3 = 3 

	dim stDim[]
		stDim[DIM1] = "제작치수"
		stDim[DIM2] = "모듈호칭치수"
		stDim[DIM3] = "개구부치수"

	!	프레임
	!----------------------------------------------------------------------------------------------------
	!	iFrame_setting 인덱스
	FRAME_SIZE_ONCE = 1						!	일괄
	FRAME_SIZE_VERTHORI = 2					!	수직/수평


	!	2D / 3D 표현
	!----------------------------------------------------------------------------------------------------
	!	'iDetail_2d' 인덱스
	DETAIL_2D_SIMPLE = 1					
	DETAIL_2D_DETAIL = 2					
	DETAIL_2D_OFF = 3	
	DETAIL_2D_MVO = 4

	dim	stDetail_2d[]
		stDetail_2d[DETAIL_2D_SIMPLE] = "단순(기본 설계)"		
		stDetail_2d[DETAIL_2D_DETAIL] = "상세(실시 설계)"		
		stDetail_2d[DETAIL_2D_OFF] = "끔"			
		stDetail_2d[DETAIL_2D_MVO] = "by MVO"	

	!	'iDetail_3d' 인덱스
	DETAIL_3D_SIMPLE = 1					
	DETAIL_3D_DETAIL = 2					
	DETAIL_3D_OFF = 3	
	DETAIL_3D_MVO = 4

	dim	stDetail_3d[]
		stDetail_3d[DETAIL_2D_SIMPLE] = "단순"		
		stDetail_3d[DETAIL_2D_DETAIL] = "상세"		
		stDetail_3d[DETAIL_2D_OFF] = "끔"			
		stDetail_3d[DETAIL_2D_MVO] = "by MVO"		

	!	'iLine_show_2d' , 'iLine_show_3d' 인덱스
	LINE_SHOW = 1
	LINE_HIDE = 2
	LINE_MVO = 3

	dim	stLine_show[]
		stLine_show[LINE_SHOW] = "보이기"
		stLine_show[LINE_HIDE] = "숨기기"
		stLine_show[LINE_MVO] = "by MVO"
		

	!	MVO 
	!----------------------------------------------------------------------------------------------------
	!	'iDetail_setting_mvo' 인덱스
	SETTING_ALL = 1
	SETTING_SCALE = 2

	iDetail_setting_mvo = 0
	success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "iDetail_setting", iDetail_setting_mvo)

	!	파라미터 스크립트 에서는 GLOB_SCALE 변수가 사용되지 않도록 조건
	if	GLOB_SCRIPT_TYPE <> 5	then

		!	일괄 설정 
		!----------------------------------------------------------------------------------------------------
		if	iDetail_setting_mvo = SETTING_ALL	then
			if	iDetail_2d = DETAIL_2D_MVO	then
				iDetail_2d_temp = 0
				success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "MVO_iDetail_2d", iDetail_2d_temp)
				iDetail_2d = iDetail_2d_temp
			endif
			if	iDetail_3d = DETAIL_3D_MVO	then
				iDetail_3d_temp = 0
				success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "MVO_iDetail_3d", iDetail_3d_temp)
				iDetail_3d = iDetail_3d_temp
			endif
		endif

		!	스케일별 설정 
		!----------------------------------------------------------------------------------------------------
		
		if	iDetail_setting_mvo = SETTING_SCALE	then
			if	iDetail_2d = DETAIL_2D_MVO | iDetail_3d = DETAIL_3D_MVO 	then

				iScale_arr_num = 0
				success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "iDetail_scale_num", iScale_arr_num)

				iScale_arr_temp = 0
				iDetail_2d_temp = -1
				iDetail_3d_temp = -1
				for i = 1	to	iScale_arr_num
					num = str(i, 1, 0)
					success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "iScale_"+num, iScale_arr_temp)
					!	현재 스케일과 저장된 스케일 값 비교 
					if	abs(GLOB_SCALE-iScale_arr_temp) < EPS	then				
						if	iDetail_2d = DETAIL_2D_MVO	then
							success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "MVO_iDetail_2d_"+num, iDetail_2d_temp)
							iDetail_2d = iDetail_2d_temp	
						endif	
						if	iDetail_3d = DETAIL_3D_MVO	then		
							success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "MVO_iDetail_3d_"+num, iDetail_3d_temp)
							iDetail_3d = iDetail_3d_temp
						endif
					endif
				next i
				if	iDetail_2d_temp = -1 & iDetail_3d_temp = -1	then
					!	지정된 스케일이 없을 경우 '기타' 스케일 정보를 적용 
					num = str(iScale_arr_num+1, 1, 0)
					if	iDetail_2d = DETAIL_2D_MVO	then
						success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "MVO_iDetail_2d_"+num, iDetail_2d_temp)
						iDetail_2d = iDetail_2d_temp				
					endif
					if	iDetail_3d = DETAIL_3D_MVO	then	
						success = LIBRARYGLOBAL ("mvo_창호 20.gsm", "MVO_iDetail_3d_"+num, iDetail_3d_temp)
						iDetail_3d = iDetail_3d_temp
					endif
				endif
			endif
		endif
		
	endif



!----------------------------------------------------------------------------------------------------
!	연산
!----------------------------------------------------------------------------------------------------
	!	창호 치수
	!---------------------------------------------------------------------------------------------------
	mWidth_frame = 0
	mHeight_frame = 0
	mWidth_opening = 0
	mHeight_opening = 0

	!	SIMPLE
	!	- frame : 선택한 치수를 따라 창호 사이즈가 결정된다.
	!	- opening : 프레임 사이즈와 동일하다.
	if	(GLOB_SCRIPT_TYPE <> 3 & (iDetail_2d = DETAIL_2D_SIMPLE | iDetail_2d = DETAIL_2D_OFF)) \
		| (GLOB_SCRIPT_TYPE <> 2 & (iDetail_3d = DETAIL_3D_SIMPLE | iDetail_3d = DETAIL_3D_OFF))	then
		if	iDim = DIM1 	then
			mWidth_frame = mWidth_1
			mHeight_frame = mHeight_1
		endif 
		if	iDim = DIM2 	then
			mWidth_frame = mWidth_2
			mHeight_frame = mHeight_2
		endif
		if	iDim = DIM3 	then
			mWidth_frame = mWidth_3
			mHeight_frame = mHeight_3
		endif

		mWidth_opening = mWidth_frame
		mHeight_opening = mHeight_frame
	endif

	!	DETAIL
	!	- frame : 제작치수를 따라 창호 사이즈가 결정된다.
	!	- opening : 개구부치수를 따라 오프닝 사이즈가 결정된다.
	if	(GLOB_SCRIPT_TYPE <> 3 & iDetail_2d = DETAIL_2D_DETAIL) \
		| (GLOB_SCRIPT_TYPE <> 2 & iDetail_3d = DETAIL_3D_DETAIL)	then
		mWidth_frame = mWidth_1
		mHeight_frame = mHeight_1
		mWidth_opening = mWidth_3
		mHeight_opening = mHeight_3		
	endif	

	mWidth = mWidth_frame
	mHeight = mHeight_frame


	!	샤시 치수
	!---------------------------------------------------------------------------------------------------
	if	iFrame_setting = FRAME_SIZE_ONCE	then
		mSash_width = mWidth - mFrame_w*2
		mSash_height = mHeight - mFrame_w*2
	endif
	if	iFrame_setting = FRAME_SIZE_VERTHORI	then
		mSash_width = mWidth - mFrame_vert_w*2
		mSash_height = mHeight - mFrame_hori_w*2
	endif


